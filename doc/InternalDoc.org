#+SETUPFILE: ../Setup/setupFile-1.org
#+TITLE: Internal documentation

* The =.dir-locals.el= file
  #+INDEX: my-project-root!.dir-locals.el

  The =.dir-locals.el= file can be used as usual, however it must exists. Here we use an empty one.

  #+BEGIN_SRC emacs-lisp :eval never :tangle yes :tangle "../.dir-locals.el"
;; Must exist because this file is used to find my-project-root
;;
;; Here contains nothing (), however you can fill it with
;; your usual stuff.
()
  #+END_SRC

  its role is to define the =my-project-root=. 

  We minimize the work done in =.dir-locals.el= as some packages like
  =magit= refresh it beafore and after commit. Instead we define a
  special function =my-project-configuration= that trigger project
  configuration on-demand. The advantage is that we control when we want
  to refresh our project configuration, the inconvenient is that we have
  to do it /manually/.

* The =my-project-configuration= function
  #+INDEX: init.el!my-project-configuration

  The =my-project-configuration= function is define as follow:

  #+BEGIN_SRC emacs-lisp 
(defun my-project-configuration ()
  "Find my-project-root as parent directory of the closest .dir-locals.el file, 
then load my-project-root/Setup/setupFile.el project configuration file "
  (interactive)
  (let ((enable-local-variables :all))
    (setq my-project-root (let ((d (dir-locals-find-file ".")))
			    (when d (file-name-directory (if (stringp d) d (car d))))))

    (if my-project-root
	(let ((my-project-setup-file (concat my-project-root "Setup/setupFile.el")))
	  (if (file-exists-p my-project-setup-file)
	      (load my-project-setup-file nil t t nil)
	    (error "Project %s setup file not found" my-project-setup-file)))
      (error "Project root dir not found (missing .dir-locals.el file)"))))
  #+END_SRC

  #+RESULTS:
  : my-project-configuration

  It must be included in your =init.el= file.

  It searches for the root =.dir-locals.el= file then load the
  =project_root/Setup/setupFile.el= file.  If =.dir-locals.el= or
  =project_root/Setup/setupFile.el= is missing the function prints an error
  message.

* Default =setupFile.el= file

  The a typical =setupFile.el= file is defined as follow:

  #+BEGIN_SRC emacs-lisp :tangle yes :tangle "../Setup/setupFile.el"
(if my-project-root
    ;; Configuration
    (progn
      (message "Configuring %s " my-project-root)
      ;;
      ;; Local bibliography
      ;;
      (setq my-bibtex-filename 
	    (concat my-project-root "Bibliography/bibliography.bib"))
      (if (file-exists-p my-bibtex-filename)
	  ;; If bibliography.bib exists 
	  (setq reftex-default-bibliography  `(,my-bibtex-filename)
		
		bibtex-completion-bibliography my-bibtex-filename
		bibtex-completion-library-path (file-name-directory my-bibtex-filename)
		bibtex-completion-notes-path (file-name-directory my-bibtex-filename)
		
		org-ref-default-bibliography  `(,my-bibtex-filename)
		org-ref-pdf-directory (file-name-directory my-bibtex-filename)
		)
	;; otherwise unbound meaningless my-bibtex-filename
	(makunbound 'my-bibtex-filename)
	)
      ;;
      ;; Agenda files
      ;;
      (setq org-agenda-files
	    (mapcar 'abbreviate-file-name
		    (split-string
		     (shell-command-to-string (format "find %s -name \"*.org\"" my-project-root))
		     "\n")))
      ;;
      ;; My my-workInProgress-filename and its associated captures
      ;;
      (setq my-workInProgress-filename (concat my-project-root "workInProgress.org"))

      (when (file-exists-p my-workInProgress-filename)
	(setq org-capture-templates
	      `(
		("t"
		 "Todo" entry (file+headline ,my-workInProgress-filename "Project TODO")
		 "* TODO %^{Title?} [/] %^G\n:PROPERTIES:\n:Created: %U\n:END:\n\n - [ ] %?"
		 :empty-lines 1  
		 :create t
		 )
		
		("T"
		 "Todo with file link" entry (file+headline ,my-workInProgress-filename "Project TODO")
		 "* TODO %^{Title|%f} [/] %^G\n:PROPERTIES:\n:Created: \
		 %U\n:END:\n\n[[%l][In file %f]]:\n\n#+BEGIN_EXAMPLE\n%i\n#+END_EXAMPLE\n\n - [ ] %?"
		 :empty-lines 1  
		 :create t
		 )
		
		("j" "Journal" entry (file+olp+datetree ,my-workInProgress-filename "Project Journal")
		 "* %^{Title} %^G\n\n%?"
		 :empty-lines 1  
		 :create t
		 )

		("J" 
		"Journal with file link" entry (file+olp+datetree ,my-workInProgress-filename "Project journal")
		 "* %^{Title|%f} %^G\n\n[[%l][In file %f]]:\n\n#+BEGIN_EXAMPLE\n%i\n#+END_EXAMPLE\n\n%?"
		 :empty-lines 1  
		 :create t
		 )
		;; CAVEAT: emacs must run in server mode for org-protocol
		;; One must use this function:
		;;
		;; (defun transform-square-brackets-to-round-ones(string-to-transform)
		;;   "Transforms [ into ( and ] into ), other chars left unchanged."
		;;   (concat 
		;;   (mapcar #'(lambda (c) (if (equal c ?[) ?\( (if (equal c ?]) ?\) c))) string-to-transform))
		;;   )
		;;
		;; See: https://github.com/sprig/org-capture-extension#set-up-handlers-in-emacs
		;; for further details
		;;
		("L" 
		"Protocol Link" entry (file+headline ,my-workInProgress-filename "W3 Links")
		"* [[%:link][%(transform-square-brackets-to-round-ones \"%:description\")]] \
		%^G\n:PROPERTIES:\n:Created: %U\n:END:\n\n%?"
		:empty-lines 1  
		:create t
		 )
		
		("p" 
		"Protocol" entry (file+headline ,my-workInProgress-filename "W3 Links")
		 "* [[%:link][%(transform-square-brackets-to-round-ones \"%:description\")]] \
		 %^G\n:PROPERTIES:\n:Created: %U\n:END:\n#+BEGIN_EXAMPLE\n%i\n#+END_EXAMPLE\n\n%?"
		 :empty-lines 1  
		 :create t
		 )
		)))
      ;;
      ;; How to publish
      ;;
      ;; Note: you can publish somewhere else, for instance:
      ;;
      ;; (setq my-publish-dir "~/Temp/Publish")
      ;;
      ;; by default we publish in-place 
      ;; (advantage: C-c C-e h h directly update the published page)
      (setq my-publish-dir my-project-root)

      (setq my-project-name "My_Project_Name")

      (setq org-publish-project-alist
	    `(
	      ("Org_Files"
	       :base-directory ,my-project-root
	       :base-extension "org"
	       :recursive t
	       :publishing-directory ,my-publish-dir
	       :publishing-function org-html-publish-to-html
	       :htmlize-source t
	       :org-html-head-include-default-style nil
	       :exclude "Setup*\\|sitemap.org"
	       :makeindex t
	       :auto-sitemap t
	       :sitemap-title ,my-project-name 
	       )

	      ;; ("PPack_Tangle"
	      ;;  :base-directory ,my-project-root
	      ;;  :base-extension "org"
	      ;;  :recursive t
	      ;;  :publishing-directory ,my-publish-dir
	      ;;  :publishing-function org-babel-tangle-publish
	      ;;  :exclude ".*bazel-.*"
	      ;;  )

	      ("Data_Files"
	       :base-directory ,my-project-root
	       :base-extension "nb\\|?pp\\|png"
	       :recursive t
	       :publishing-directory ,my-publish-dir
	       :publishing-function org-publish-attachment
	       :exclude ".*bazel-.*"
	       )

	      ;; Main
	      (,my-project-name
	       :components ("Org_Files"
			    ;;"DefaultProject_Tangle"
			    "Data_Files")
	       )
	      )
	    )
      ) ; progn
  ;; else
  (error "Project root undefined")
  )
  #+END_SRC

